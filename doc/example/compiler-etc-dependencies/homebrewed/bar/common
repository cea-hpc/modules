# Common stuff for modulefile for package bar
# Using "homebrewed flavors" strategy
#
# This file expects the following Tcl variables to have been
# previously set:
#	version: version of bar

# Declare the path where the packages are installed
# The reference to the environment variable is a hack
# for this example; normally one would hard code a path
set rootdir $::env(MOD_GIT_ROOTDIR)
set swroot $rootdir/doc/example/compiler-etc-dependencies/dummy-sw-root

# Also get location of and load common procedures
# This is a hack for the cookbook examples, in production
# one should either
# 1) declare the procedures in a site config file (preferred)
# 2) hardcode the path to $tcllibdir and common_utilities.tcl
set tcllibdir $rootdir/doc/example/compiler-etc-dependencies/tcllib
source $tcllibdir/common_utilities.tcl

proc ModulesHelp { } {
   puts stderr "
bar: Test dummy version of bar $version

For testing packages depending on compilers/MPI

"
}

module-whatis "Dummy bar $version"

# Figure out what compiler we have loaded
# Will default to and load default compiler if none loaded
set ctag [ GetLoadedCompiler 1 1 1 ]

# Figure out what simd is loaded
set simd [ GetTagOfModuleLoaded simd ]
# If no simd loaded, default to sse4.1
if { $simd eq {} } {
   set simd simd/sse4.1
   # We will load this so autohandling knows is a dependency
   module load $simd
   PrintLoadInfo "Setting simd to $simd"
}
# Just get version of simd
set simdVersion [ lindex [ split $simd / ] 1 ]

# Compute the installation prefix
set pkgroot $swroot/bar
set vroot $pkgroot/$version
set prefix $vroot/$ctag/$simdVersion

# Make sure there is a build for this foo version/compiler/simd
# I.e. that the prefix exists
if ![ file exists $prefix ] {
   # Not built for this compiler/MPI, alert user and abort
   PrintLoadError "
foo/$version does not appear to be built for compiler $ctag and $simd

Please select a different openmpi version or different compiler/simd combination.
"
}

# We need to prereq the compiler to allow autohandling to work
prereq $ctag
# and simd
prereq $simd

# Set environment variables
setenv FOO_DIR $prefix

set bindir $prefix/bin
set libdir $prefix/lib
set incdir $prefix/include

prepend-path PATH            $bindir
prepend-path LIBRARY_PATH    $libdir
prepend-path LD_LIBRARY_PATH $libdir
prepend-path CPATH           $incdir

conflict bar
