#
# Tcsh commandline completion
#

alias _module_avail '\\
    @TCLSH@ @libexecdir@/modulecmd.tcl tcsh avail --color=never -s -t -S |& sed '"'"'\\
        /^-\+/d; /^\s*$/d; \\
        /->.*$/d; \\
        /:$/d; \\
        s#^\(.*\)/\(.\+\)(.*default.*)#\1\n\1\/\2#; \\
        s#(.*)$##g; \\
        s#\s*$##g; \\
        s#/*$##g; '"'"

alias _module_loaded '\\
    @TCLSH@ @libexecdir@/modulecmd.tcl tcsh list --color=never -s -t |& sed '"'"'\\
        /No Modulefiles Currently Loaded\.$/d; \\
        /^-\+/d; /^\s*$/d; \\
        /->.*$/d; \\
        /:$/d; \\
        s#^\(.*\)/\(.\+\)(.*default.*)#\1\n\1\/\2#; \\
        s#(.*)$##g; \\
        s#/*$##g; '"'"

alias _module_savelist '\\
    @TCLSH@ @libexecdir@/modulecmd.tcl tcsh savelist --color=never -s -t |& sed '"'"'\\
        /No named collection\.$/d; \\
        /Named collection list$/d; \\
        /:$/d; '"'"

alias _module_not_yet_loaded '\\
        mkfifo /tmp/modules_tcsh_completion.$$.p1 /tmp/modules_tcsh_completion.$$.p2 && \\
        ( _module_avail | sort >! /tmp/modules_tcsh_completion.$$.p1 & ); \\
        ( _module_loaded | sort >! /tmp/modules_tcsh_completion.$$.p2 & ); \\
        comm -23 /tmp/modules_tcsh_completion.$$.p1 /tmp/modules_tcsh_completion.$$.p2; \\
        \rm /tmp/modules_tcsh_completion.$$.p1 /tmp/modules_tcsh_completion.$$.p2'

alias _module_modulepath 'echo ${MODULEPATH} | sed '"'"'s/:/\n/g;'"'"' '

set module_cmds = "add apropos aliases avail append-path clear config del display help initadd initclear initlist initprepend initrm is-loaded is-saved is-used is-avail info-loaded keyword list load path paths purge prepend-path refresh reload restore rm remove remove-path save savelist saveshow saverm search show source swap switch test unload unuse use whatis"

set module_opts = "-D -h -s -v -V --debug --help --silent --verbose --version --paginate --no-pager --color --color="
set module_load_opts = "--auto --no-auto --force -f --icase -i"
set module_list_opts = "-l -t --long --terse"
set module_clear_opts = "--force -f"
set module_avail_opts = "-C -d -i -L -l -S -t --contains --default --icase --latest --long --starts-with --terse --indepth --no-indepth"
set module_mfile_opts = "-i --icase"
set module_path_opts = "-d --delim --duplicates"
set module_rm_path_opts = "-d --delim --index"
set module_config_opts = "--dump-state --reset advanced_version_spec auto_handling avail_indepth collection_pin_version collection_target color colors contact extended_default extra_siteconfig home icase implicit_default locked_configs pager rcfile run_quarantine search_match set_shell_startup silent_shell_debug term_background unload_match_order verbosity wa_277"

complete module 'n/help/`_module_avail`/' \
        'n/add/`_module_not_yet_loaded; echo ${module_load_opts}`/' \
        'n/load/`_module_not_yet_loaded; echo ${module_load_opts}`/' \
        'n/rm/`_module_loaded; echo ${module_load_opts}`/' \
        'n/del/`_module_loaded; echo ${module_load_opts}`/' \
        'n/remove/`_module_loaded; echo ${module_load_opts}`/' \
        'n/unload/`_module_loaded; echo ${module_load_opts}`/' \
        'n/swap/`_module_loaded; echo ${module_load_opts}`/' \
        'N/swap/`_module_not_yet_loaded; echo ${module_load_opts}`/' \
        'n/switch/`_module_loaded; echo ${module_load_opts}`/' \
        'N/switch/`_module_not_yet_loaded; echo ${module_load_opts}`/' \
        'n/show/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/display/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/test/`_module_avail; echo ${module_mfile_opts}`/' \
        "n/list/(${module_list_opts})/" \
        'n/avail/`_module_avail; echo ${module_avail_opts}`/' \
        'n/is-avail/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/is-loaded/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/info-loaded/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/restore/`_module_savelist`/' \
        "n/savelist/(${module_list_opts})/" \
        'n/saveshow/`_module_savelist`/' \
        'n/saverm/`_module_savelist`/' \
        'n/save/`_module_savelist`/' \
        'n/is-saved/`_module_savelist`/' \
        'n/aliases/n/' \
        'n/use/d/' \
        'N/use/d/' \
        'n/unuse/`_module_modulepath`/' \
        'n/is-used/`_module_modulepath`/' \
        'n/purge/n/' \
        'n/refresh/n/' \
        'n/reload/n/' \
        'n/whatis/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/apropos/n/' \
        'n/search/n/' \
        'n/keyword/n/' \
        'n/paths/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/path/`_module_avail; echo ${module_mfile_opts}`/' \
        'n/source/n/' \
        'n/initadd/n/' \
        'n/initprepend/n/' \
        'n/initrm/n/' \
        'n/initlist/n/' \
        'n/initclear/n/' \
        "n/append-path/(${module_path_opts})/" \
        "n/prepend-path/(${module_path_opts})/" \
        "n/remove-path/(${module_rm_path_opts})/" \
        "n/clear/(${module_clear_opts})/" \
        "n/config/(${module_config_opts})/" \
        "n/--reset/(${module_config_opts})/" \
        'n/-h/n/' \
        'n/--help/n/' \
        'n/-V/n/' \
        'n/--version/n/' \
        'C/sw*/(switch)/' \
        "p/1/(${module_cmds} ${module_opts})/" \
        "n/-*/(${module_cmds})/" 

