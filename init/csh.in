if ($?tcsh) then
	set modules_shell="tcsh"
else
	set modules_shell="csh"
endif
if ( $?MODULE_VERSION ) then
	setenv MODULE_VERSION_STACK 	"$MODULE_VERSION"
else
	setenv MODULE_VERSION		"@VERSION@"
	setenv MODULE_VERSION_STACK	"@VERSION@"
endif
set exec_prefix='@BASEPREFIX@$MODULE_VERSION'

if ( $?histchars ) then
  set _histchars = $histchars
  if ($?prompt) then
    alias module 'unset histchars;set _prompt="$prompt";eval `'$exec_prefix'/bin/modulecmd '$modules_shell \!*`;set histchars = $_histchars; set prompt="$_prompt";unset _prompt'
  else
  alias module 'unset histchars;eval `'$exec_prefix'/bin/modulecmd '$modules_shell \!*`;set histchars = $_histchars'
  endif
else
  if ($?prompt) then
    alias module 'set _prompt="$prompt";set prompt="";eval `'$exec_prefix'/bin/modulecmd '$modules_shell' \!*`;set prompt="$_prompt";unset _prompt'
else
  alias module 'eval `'$exec_prefix'/bin/modulecmd '$modules_shell' \!*`'
  endif
endif
unset exec_prefix

setenv MODULESHOME @prefix@

if (! $?MODULEPATH ) then
  setenv MODULEPATH `sed 's/#.*$//' ${MODULESHOME}/init/.modulespath | awk 'NF==1{printf("%s:",$1)}'`
endif

if (! $?LOADEDMODULES ) then
  setenv LOADEDMODULES ""
endif
